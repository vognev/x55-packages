# Maintainer: Vitaliy Ognev <vitaliy.ognev@gmail.com>

pkgbase=linux-x55

pkgname=(
    $pkgbase
    $pkgbase-headers
)

_basekernel=6.7
pkgver=6.7.rc5.arch1
pkgrel=1
pkgdesc='Linux X55'
arch=(aarch64)
license=(GPL2)
makedepends=(
  bc
  wireless-regdb
)

options=('!strip')

source=(
  https://git.kernel.org/torvalds/t/linux-6.7-rc5.tar.gz
  linux-x55.preset
  6.7.patch
  6.7.config
)

md5sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
)

export KBUILD_BUILD_HOST=archlinux
export KBUILD_BUILD_USER=$pkgbase
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"
export ARCH=arm64

DEVICE_DTB="rk3566-powkiddy-x55"

prepare() {
  cd "$srcdir/linux-6.7-rc5"

  patch -p1 < ../6.7.patch

  # don't run depmod on 'make install'. We'll do this ourselves in packaging
  sed -i '2iexit 0' scripts/depmod.sh

  # copy over config
  cp ../6.7.config .config
  make olddefconfig
  diff -u ../6.7.config .config || :
}

build() {
  cd "$srcdir/linux-6.7-rc5"
  make ${MAKEFLAGS} Image modules rockchip/$DEVICE_DTB.dtb
}

package_linux-x55() {
  pkgdesc="The $pkgdesc kernel and modules"
  depends=(
    coreutils
    initramfs
    kmod
  )
  optdepends=(
    'wireless-regdb: to set the correct wireless channels of your country'
    'linux-firmware: firmware images needed for some devices'
  )
  provides=(
    KSMBD-MODULE
    VIRTUALBOX-GUEST-MODULES
    WIREGUARD-MODULE
  )
  replaces=(
    virtualbox-guest-modules-arch
    wireguard-arch
  )

  cd "$srcdir/linux-6.7-rc5"

  local _version=$(make kernelrelease)
  local modulesdir="${pkgdir}/usr/lib/modules/${_version}"

  install -Dm644 arch/arm64/boot/Image -T  "${pkgdir}/boot/vmlinuz-linux-x55"
  install -Dm644 arch/arm64/boot/dts/rockchip/$DEVICE_DTB.dtb -Dt "${pkgdir}/boot"
  make INSTALL_MOD_PATH="${pkgdir}/usr" modules_install

  # remove build and source links
  rm -f "${pkgdir}"/usr/lib/modules/${_version}/{source,build}
  # remove the firmware
  rm -rf "${pkgdir}/usr/lib/firmware"

  # Now we call depmod...
  depmod -b "${pkgdir}/usr" -F System.map "$_version"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"
  cat ../linux-x55.preset | sed "s/@version@/${_version}/" \
    | install -Dm644 /dev/stdin "${pkgdir}/etc/mkinitcpio.d/${pkgbase}.preset"
}

package_linux-x55-headers() {
  pkgdesc="Header files and scripts for building modules for linux kernel - ${_desc}"
  provides=("linux-headers=${pkgver}")
  conflicts=('linux-headers')
  depends=(
    bc
  )

  cd "$srcdir/linux-6.7-rc5"
  local _version=$(make kernelrelease)
  local builddir="$pkgdir/usr/lib/modules/${_version}/build"

  install -Dt "$builddir" -m644 .config Module.symvers System.map
  make mrproper

  find . -name 'Kconfig*'  -exec install -Dm644 {} "$builddir/{}" \;
  find . -name 'Makefile*' -exec install -Dm644 {} "$builddir/{}" \;
  find . -name 'Kbuild*'   -exec install -Dm644 {} "$builddir/{}" \;
  cp -av --parents -t "$builddir" include/ arch/arm64/ kernel/ lib/ tools/ security/ scripts/
  find -L "$builddir" -type l -printf 'Removing %P\n' -delete
  find    "$builddir" -type f -printf 'Removing %P\n' -name '*.o' -delete
}
