# Maintainer: Vitaliy Ognev <vitaliy.ognev@gmail.com>

pkgname=uboot-x55
pkgver=v2017.09
pkgrel=1
pkgdesc='U-Boot X55'
arch=(aarch64)
license=(GPL2)
options=('!strip')

makedepends=(
  bc
  parted
)

source=(
    u-boot::git+https://github.com/rockchip-linux/u-boot#commit=32640b0ada
    rk3566_ddr_1056MHz_v1.18.bin
    rk356x_spl_loader_v1.18.112.bin
    rk3568_bl31_v1.43.elf
    rk3568_bl32_v2.10.bin
    x55-uboot.patch
)

md5sums=(
    SKIP
    fe229b928a6df80723fa60080532b510
    02c554962f0e7f459b821ca447dcc344
    281023b535185dba5433d921738832b8
    44f877fcfb78060f391724512b44f502
    SKIP
)

prepare() {
    cd "$srcdir/u-boot"

    git apply "$srcdir/x55-uboot.patch"

    LDFLAGS="" ARCH=arm64 make mrproper
    LDFLAGS="" ARCH=arm64 make rk3568_defconfig
}

build() {
    cd "$srcdir/u-boot"

    cp "$srcdir/rk3568_bl31_v1.43.elf" "bl31.elf"
    cp "$srcdir/rk3568_bl32_v2.10.bin" "tee.bin"

    LDFLAGS="" ARCH=arm64 make u-boot.dtb u-boot.itb \
        CONFIG_MKIMAGE_DTC_PATH="scripts/dtc/dtc"
}

package() {
    cd "$srcdir/u-boot"

    local dest="$pkgdir/usr/share/bootloader"
    install -d $dest

    tools/mkimage -n rk3568 -T rksd -d $srcdir/rk3566_ddr_1056MHz_v1.18.bin:$srcdir/rk356x_spl_loader_v1.18.112.bin -C bzip2 "$dest/idbloader.img"

    install -t "$dest" u-boot.itb
    install -d "$dest/extlinux"

    cat << EOF > "$dest/extlinux/extlinux.conf"
LABEL X55Arch
  LINUX /vmlinuz-linux-x55
  FDTDIR /
  APPEND boot=UUID=@UUID_SYSTEM@ disk=UUID=@UUID_STORAGE@ quiet rootwait console=ttyUSB0,1500000 fbcon=rotate:3 console=tty0 ssh consoleblank=0 systemd.show_status=0 loglevel=0 panic=20
EOF

}
