# Maintainer: $USER

pkgname=uboot-x55
pkgver=v2024.01.rc4
pkgrel=1
pkgdesc='U-Boot X55'
arch=(aarch64)
license=(GPL2)
options=('!strip')

makedepends=(
  swig
  python-pyelftools
)

source=(
    u-boot::git+https://github.com/u-boot/u-boot.git#commit=27089f1e4d
    rk3566_ddr_1056MHz_v1.18.bin
    rk356x_spl_loader_v1.18.112.bin
    rk3568_bl31_v1.43.elf
    rk3568_bl32_v2.10.bin
    powkiddy-x55.patch
)

md5sums=(
    SKIP
    fe229b928a6df80723fa60080532b510
    02c554962f0e7f459b821ca447dcc344
    281023b535185dba5433d921738832b8
    44f877fcfb78060f391724512b44f502
    SKIP
)

prepare() {
    cd "$srcdir/u-boot"

    git apply "$srcdir/powkiddy-x55.patch"

    LDFLAGS="" ARCH=arm64 make mrproper
    LDFLAGS="" ARCH=arm64 make powkiddy-x55-rk3566_defconfig
}

build() {
    cd "$srcdir/u-boot"

    export ROCKCHIP_TPL=../rk3566_ddr_1056MHz_v1.18.bin
    export BL31=../rk3568_bl31_v1.43.elf

    LDFLAGS="--lssl -lcrypto"   ARCH=arm64 make spl/u-boot-spl.bin u-boot.dtb u-boot.img tools
    LDFLAGS=""                  ARCH=arm64 make CONFIG_MKIMAGE_DTC_PATH="scripts/dtc/dtc"

    tools/mkimage -n rk3568 -T rksd -d ../rk3566_ddr_1056MHz_v1.18.bin:../rk356x_spl_loader_v1.18.112.bin -C bzip2 idbloader.img
}

package() {
    local dest="$pkgdir/usr/share/bootloader"
    cd "$srcdir/u-boot"

    install -d "$dest"
    install -t "$dest" u-boot.itb idbloader.img
    cat << EOF > "$dest/extlinux.conf"
LABEL X55Arch
  LINUX /vmlinuz-linux-x55
  FDTDIR /
  APPEND root=UUID=@@UUID_ROOT@@ rootwait rw initrd=/initramfs-linux-x55.img console=ttyS2,1500000 console=tty0 loglevel=7
EOF

}
